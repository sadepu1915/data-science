
* repeat.sas;
option ls=75 ps=55 nocenter nodate; 
title 'ANALYSIS OF REPEATED MEASURES DESIGN';

data ASPAR; 
INPUT BLOCK C $ @;
DO Y = 1930 TO 1933;
INPUT W @; OUTPUT; END;
LABEL Y = 'YEAR' C = 'DATE OF LAST CUTTING' W='WEIGHT';
cards;                                                             
1 JN1   
230 324 512 399
1 JN15 
212 415 584 386
1 JL1 
183 320 456 255
1 JL15
148 246 304 144
2 JN1
216 317 448 361
2 JN15
190 296 471 280
2 JL1
186 295 387 187
2 JL15
126 201 289 83
3 JN1
219 357 496 344
3 JN15
151 278 399 254
3 JL1
177 298 427 239
3 JL15
107 192 271 90
4 JN1
200 362 540 381
4 JN15
150 336 485 279
4 JL1
209 328 462 244
4 JL15
168 226 312 168
RUN;
PROC PRINT;
RUN;
DATA EFFECTS; SET ASPAR;
YL = -3*(Y=1930) - (Y=1931) + (Y=1932) + 3*(Y=1933);
YQ = (Y=1930) - (Y=1931) - (Y=1932) + (Y=1933);
YC = -(Y=1930) +3*(Y=1931) -3*(Y=1932) + (Y=1933);
CL = -3*(C='JN1') - (C='JN15') + (C='JL1') + 3*(C='JL15');
CQ = (C='JN1') - (C='JN15') - (C='JL1') + (C='JL15');
CC = -(C='JN1') +3*(C='JN15') -3*(C='JL1') + (C='JL15');
CLYL=CL*YL; CLYQ=CL*YQ; CLYC=CL*YC;
CQYL=CQ*YL; CQYQ=CQ*YQ; CQYC=CQ*YC;
CCYL=CC*YL; CCYQ=CC*YQ; CCYC=CC*YC;
RUN;
PROC GLM;
CLASS BLOCK C Y;
MODEL W=BLOCK|C|Y/SS3;
TEST H=C E=BLOCK*C;
TEST H=Y E=BLOCK*Y;
TEST H=Y*C E=BLOCK*C*Y;
RUN;
PROC REG; 
MODEL W=CL CQ CC YL YQ YC CLYL CLYQ CLYC CQYL CQYQ CQYC CCYL CCYQ CCYC/SS1 SS3;
RUN;
PROC GLM; CLASS BLOCK C;
MODEL W = BLOCK|C YL YL*BLOCK YL*C YL*C*BLOCK/SS3;
TEST H=YL*C E=YL*C*BLOCK;
RUN;
PROC GLM; CLASS BLOCK C;
MODEL W = BLOCK|C YQ YQ*BLOCK YQ*C YQ*C*BLOCK/SS3;
TEST H=YQ*C E=YQ*C*BLOCK;
RUN;
PROC GLM; CLASS BLOCK C;
MODEL W = BLOCK|C YC YC*BLOCK YC*C YC*C*BLOCK/SS3;
TEST H=YC*C E=YC*C*BLOCK;
RUN;
